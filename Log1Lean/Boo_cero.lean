import Mathlib.Tactic

/-
# Ãlgebras de Boole "desde cero"
-/

class Boo (B : Type) where
  --Estructura:
  uno : B
  cero : B
  inf : B â†’ B â†’ B
  sup : B â†’ B â†’ B
  comp : B â†’ B
  --Axiomas:
  inf_conm : âˆ€ x y : B, inf x y = inf y x
  inf_asoc : âˆ€ x y z : B, inf (inf x y) z = inf x (inf y z)
  inf_idem : âˆ€ x : B, inf x x = x
  sup_conm : âˆ€ x y : B, sup x y = sup y x
  sup_asoc : âˆ€ x y z : B, sup (sup x y) z = sup x (sup y z)
  sup_idem : âˆ€ x : B, sup x x = x
  abso : âˆ€ x y : B, sup (inf x y) y = y
  inf_sup : âˆ€ x y z : B, inf x (sup y z) = sup (inf x y) (inf x z)
  inf_comp : âˆ€ x : B, inf x (comp x) = cero
  sup_comp : âˆ€ x : B, sup x (comp x) = uno
  sup_cero : âˆ€ x : B, sup x cero = x
  inf_uno : âˆ€ x : B, inf x uno = x

--notaciÃ³n
notation "ğŸ™" => Boo.uno
notation "ğŸ˜" => Boo.cero
infixl:70 "âŠ“" => Boo.inf
infixl:70 "âŠ”" => Boo.sup
postfix:max "â»Â¹" => Boo.comp

--ejemplo
variable (Î± : Type)
instance : Boo (Set Î±) where
  uno := Set.univ
  cero := âˆ…
  inf := (Â· âˆ© Â·)
  sup := (Â· âˆª Â·)
  comp := (Â·)á¶œ
  inf_conm := Set.inter_comm
  inf_asoc := Set.inter_assoc
  inf_idem := Set.inter_self
  sup_conm := Set.union_comm
  sup_asoc := Set.union_assoc
  sup_idem := Set.union_self
  abso := by
    intro a b; ext x; constructor
    Â· rintro (âŸ¨xa, xbâŸ© | xb)
      assumption'
    Â· intro xb
      right; assumption
  inf_sup := Set.inter_union_distrib_left
  inf_comp := Set.inter_compl_self
  sup_comp := Set.union_compl_self
  sup_cero := Set.union_empty
  inf_uno := Set.inter_univ

open Boo
variable {B : Type} [Boo B]
variable (x y z : B)

--algunos resultados
theorem inf_cero : x âŠ“ ğŸ˜ = ğŸ˜ := by
  nth_rw 1 [<-inf_comp x]
  rw [<-inf_asoc, Boo.inf_idem, inf_comp]

theorem sup_uno : x âŠ” ğŸ™ = ğŸ™ := by
  nth_rw 1 [<-Boo.sup_comp x]
  rw [<-sup_asoc, Boo.sup_idem, sup_comp]

theorem absoâ‚‚ : (x âŠ” y) âŠ“ y = y := by
  rw [inf_conm, inf_sup, Boo.inf_idem, inf_conm, abso]

theorem sup_inf : x âŠ” (y âŠ“ z) = (x âŠ” y) âŠ“ (x âŠ” z) := by
  rw [inf_sup]
  nth_rw 3 [sup_conm]
  rw [absoâ‚‚]
  nth_rw 2 [inf_conm]
  rw [inf_sup, <-sup_asoc]
  nth_rw 3 [sup_conm]
  rw [abso]
  nth_rw 2 [inf_conm]

theorem comp_un (hci : x âŠ“ z = ğŸ˜) (hbi : x âŠ“ y = ğŸ˜)
    (hcs : x âŠ” z = ğŸ™) (hbs : x âŠ” y = ğŸ™) : y = z := by
  rw [<-inf_uno y, <-hcs, inf_sup, inf_conm, hbi]
  rw [<-hci,inf_conm, inf_conm y, <-inf_sup, hbs, inf_uno]


#check Boo.inf_comp
--leyes de De Morgan
theorem DM_inf : (x âŠ“ y)â»Â¹ = xâ»Â¹ âŠ” yâ»Â¹ := by
  have co1 : (x âŠ“ y) âŠ“ (xâ»Â¹ âŠ” yâ»Â¹) = cero := by
    rw [inf_asoc, inf_sup, inf_comp, sup_cero]
    rw [inf_conm, inf_asoc, inf_conm xâ»Â¹]
    rw [inf_comp, inf_cero]
  have co2 : (x âŠ“ y) âŠ” (xâ»Â¹ âŠ” yâ»Â¹) = ğŸ™ := by
    rw [<-sup_asoc, sup_conm (x âŠ“ y), sup_inf]
    rw [sup_conm _ x, sup_comp x, inf_conm, inf_uno]
    rw [sup_asoc, sup_comp, sup_uno]
  apply comp_un (x âŠ“ y)
  Â· apply co1
  Â· apply inf_comp
  Â· apply co2
  Â· apply sup_comp

theorem DM_sup : (x âŠ” y)â»Â¹ = xâ»Â¹ âŠ“ yâ»Â¹ := by
  have co_inf : (x âŠ” y) âŠ“ (xâ»Â¹ âŠ“ yâ»Â¹) = ğŸ˜ := by
    rw [<-inf_asoc, inf_conm (x âŠ” y), inf_sup]
    rw [inf_conm _ x, inf_comp, sup_conm, sup_cero]
    rw [inf_asoc, inf_comp, inf_cero]
  have co_sup : (x âŠ” y) âŠ” (xâ»Â¹ âŠ“ yâ»Â¹) = ğŸ™ := by
    rw [sup_asoc, sup_inf, sup_comp, inf_uno]
    rw [sup_conm, sup_asoc, sup_conm _ x, sup_comp, sup_uno]
  apply comp_un (x âŠ” y)
  Â· apply co_inf
  Â· apply inf_comp
  Â· apply co_sup
  Â· apply sup_comp

theorem comp_comp : (xâ»Â¹)â»Â¹ = x := by
  apply comp_un xâ»Â¹
  Â· rw [inf_conm, inf_comp]
  Â· rw [inf_comp]
  Â· rw [sup_conm, sup_comp]
  Â· rw [sup_comp]


--## Orden

def men : B â†’ B â†’ Prop := fun x y â†¦ x âŠ“ y = x

--sup a b es un supremo
example : men x (x âŠ” y) := by
  rw [men]
  rw [inf_sup, Boo.inf_idem, sup_conm, inf_conm, abso]


example (ha : men x z) (hb : men y z) : men (x âŠ” y) z := by
  rw [men] at *
  rw [inf_conm, inf_sup, inf_conm, ha, inf_conm, hb]

--inf a b es un Ã­nfimo
example : men (x âŠ“ y) x := by
  rw [men, inf_conm,<-inf_asoc, Boo.inf_idem]

example (ha : men z x) (hb : men z y) : men z (x âŠ“ y) := by
  rw [men] at *
  rw [<-inf_asoc, ha, hb]

--equivalencias del orden
theorem men_sup : men x y â†” x âŠ” y = y := by
  rw [men]
  constructor
  Â· intro h
    rw [<-h, sup_conm, sup_inf, Boo.sup_idem, sup_conm, absoâ‚‚]
  Â· intro h
    rw [<-h, inf_conm, sup_conm, absoâ‚‚]

theorem men_comp : men x y â†” x âŠ“ yâ»Â¹ = ğŸ˜ := by
  rw [men]
  constructor
  Â· intro h
    rw [<-h, inf_asoc, inf_comp, inf_cero]
  Â· intro h
    have hb : y âŠ” (x âŠ“ yâ»Â¹) = y := by
      rw [h, sup_cero]
    rw [sup_inf,sup_comp,inf_uno,sup_conm,<-men_sup,men] at hb
    assumption

--men es un orden parcial
theorem men_refl : men x x := by
  rw [men, Boo.inf_idem]

theorem men_tran (ab : men x y) (bc : men y z) : men x z := by
  rw [men] at *
  rw [<- ab, inf_asoc, bc]

theorem men_anti (ab : men x y) (ba : men y x) : x = y := by
  rw [men] at *
  rw [<-ab, inf_conm]
  assumption

--criterios de igualdad
lemma men_izq (h : âˆ€ w : B, men x w â†’ men y w) : men y x := by
  specialize h x
  apply h
  apply men_refl

lemma men_der (h : âˆ€ w, men w x â†’ men w y) : men x y := by
  specialize h x
  apply h
  apply men_refl

theorem ig_izq (h : âˆ€ w, men x w â†” men y w) : x = y := by
  have h' := h
  specialize h x
  specialize h' y
  apply men_anti
  Â· apply h'.2
    apply men_refl
  Â· apply h.1
    apply men_refl

theorem ig_der (h : âˆ€ w, men w x â†” men w y) : x = y := by
  have h' := h
  specialize h x
  specialize h' y
  apply men_anti
  Â· apply h.1
    apply men_refl
  Â· apply h'.2
    apply men_refl

lemma men_inf_men (h : men y z) : men (x âŠ“ y) (x âŠ“ z) := by
  rw [men] at *
  rw [inf_asoc, inf_conm y, inf_asoc, inf_conm z]
  rw [h, <-inf_asoc, Boo.inf_idem]

lemma men_sup_men (h : men y z) : men (x âŠ” y) (xâŠ” z) := by
  rw [men_sup] at *
  rw [sup_asoc, sup_conm y, sup_asoc, sup_conm z]
  rw [h, <-sup_asoc, Boo.sup_idem]

lemma men_comp_men (h : men x y) : men yâ»Â¹ xâ»Â¹ := by
  rw [men_sup] at h
  rw [men, <-DM_sup, sup_conm, h]

--### Agregar el orden a la estructura de Ã¡lgebra
class BooOrd (Î± : Type) extends Boo Î± where
  mi : Î± â†’ Î± â†’ Prop
  mi_def : âˆ€ u v : Î±, mi u v â†” u âŠ“ v = u

infixl:50 "â‰¤" => BooOrd.mi


--### Filtros
@[ext]
structure Filtro (Î± : Type) [BooOrd Î±] where
  carrier : Set Î±
  uno_en : ğŸ™ âˆˆ carrier
  inf_en {u v} : u âˆˆ carrier â†’ v âˆˆ carrier â†’ u âŠ“ v âˆˆ carrier
  mi_en {u v} : u âˆˆ carrier â†’ u â‰¤ v â†’ v âˆˆ carrier

instance [BooOrd Î±] : SetLike (Filtro Î±) Î± where
  coe := Filtro.carrier
  coe_injective' _ _ := Filtro.ext

--intersecciÃ³n de dos filtro es filtro
instance [BooOrd Î±] : Min (Filtro Î±) :=
  âŸ¨fun Fâ‚ Fâ‚‚ â†¦
    {carrier := Fâ‚ âˆ© Fâ‚‚
     uno_en := âŸ¨Fâ‚.uno_en, Fâ‚‚.uno_enâŸ©
     inf_en := fun âŸ¨hu, hu'âŸ© âŸ¨hv, hv'âŸ© â†¦ âŸ¨Fâ‚.inf_en hu hv, Fâ‚‚.inf_en hu' hv'âŸ©
     mi_en := fun âŸ¨hu, hu'âŸ© h â†¦ âŸ¨Fâ‚.mi_en hu h, Fâ‚‚.mi_en hu' hâŸ©
  }âŸ©



--## Morfismos

@[ext]
class HomBoo (F : Type) (A B : outParam Type) [Boo A] [Boo B] where
  toFun : F â†’ A â†’ B
  pre_inf : âˆ€ (f : F) (aâ‚ aâ‚‚ : A), toFun f (aâ‚ âŠ“ aâ‚‚) = (toFun f aâ‚) âŠ“ (toFun f aâ‚‚)
  pre_sup : âˆ€ (f : F) (aâ‚ aâ‚‚ : A), toFun f (aâ‚ âŠ” aâ‚‚) = (toFun f aâ‚) âŠ” (toFun f aâ‚‚)
  pre_comp : âˆ€ (f : F) (aâ‚ : A), toFun f (aâ‚â»Â¹) = (toFun f aâ‚)â»Â¹

instance [Boo A] [Boo B] [HomBoo F A B] : CoeFun F (fun _ â†¦ A â†’ B) where
  coe := HomBoo.toFun

attribute [coe] HomBoo.toFun


variable {A F G : Type} [Boo A] [HomBoo F A B] [HomBoo G B A]
variable (f : F) (g g' : G)
variable (a a1 a2 : A) (b : B)

open HomBoo

example : f ğŸ™ = ğŸ™ := by
  rw [<-sup_comp a, pre_sup, pre_comp, sup_comp]

example : f ğŸ˜ = ğŸ˜ := by
  rw [<-inf_comp a, pre_inf, pre_comp, inf_comp]

example (h : men a1 a2) : men (f a1) (f a2) := by
  rw [men] at *
  rw [<-pre_inf, h]

def adj : F â†’ G â†’ Prop :=
  fun f g â†¦ âˆ€ (a : A) (b : B), men (f a) b â†” men a (g b)

#print adj

example (h1 : adj f g) (h2 : adj f g') : toFun g = toFun g' := by
  rw [adj] at *
  funext z
  apply ig_der
  intro w
  specialize h1 w z
  specialize h2 w z
  rw [<-h1, <-h2]
